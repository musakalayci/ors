birim çizelge 
{
  dahili örs::merkez::c::stdio; 
  dahili örs::merkez::dizi;
  
  kalıp kök x,y
  {
    Sıradaki *kök'x,y;
    hash      d32;
    ad        x; 
    öz        y;
  }

  kalıp k x,y 
  {
    boyut      tam; 
    hacim      doğal; 
    yığın      dizi::k'*kök'x,y;
    Nesneler **kök'x,y;
  }
  

  tür i *k't32, şey;



  Çizelge *k'x,y
  küresel sanal iş 
  Yapılandır hacim tam 
  {
    Çizelge->hacim = hacim; 
    Çizelge->boyut = 0; 
    Çizelge->yığın.Yapılandır(Çizelge->hacim); 
    Çizelge->Nesneler = temiz(%*Çizelge->Nesneler[hacim]);
  } 

  Çizelge *k'x, y
  küresel sanal iş 
  Temizle 
  {
    her i := 0; i < Çizelge->yığın.boyut; i++: 
    {
      Kök := Çizelge->yığın.Nesneler[i]; 
      sil Kök;
    }
    Çizelge->yığın.Temizle(); 
    sil &Çizelge->Nesneler; 
  }

  küresel iş Sıra Girdi şey, no d32 : d32 
  {
    çizelge := <i>Girdi; 
    i       := küme::BernsteinD32(<*d8>&no);
    dön küme::p(i, 0, çizelge->hacim);
  }
  
  Çizelge *k'x,y 
  sanal iş kökYenile Kök *kök'x,y 
  {
    sıra             := Sıra(&Çizelge, Kök->ad); 
    Kök->Sıradaki     = Çizelge->Nesneler[sıra]; 
    Çizelge->Nesneler[sıra] = Kök; 
    Çizelge->boyut++;
  }

  Çizelge *k'x,y 
  küresel sanal iş Yenile 
  {
    Eskiler    := Çizelge->Nesneler;  
    eskiHacim  := Çizelge->hacim;
    Çizelge->hacim   *= 2;  
    Çizelge->Nesneler = temiz(%*Çizelge->Nesneler[Çizelge->hacim]); 
    Çizelge->boyut    = 0; 
    her i := 0; i < Çizelge->yığın.boyut; i++: 
    {
      Eleman          := Çizelge->yığın.Nesneler[i]; 
      Eleman->Sıradaki = boş; 
      Çizelge->kökYenile(Eleman);
    }
    sil Eskiler;
  }

  Çizelge *k'x,y 
  küresel sanal iş Ara no d32 : şey =>
    her Kök := Çizelge->Nesneler[Sıra(&Çizelge, no)]; 
        Kök; 
        Kök  = Kök->Sıradaki: 
      eğer Kök->ad == no: 
        dön  Kök->öz;

  /*öz k'x,y 
  sanal iş Gez İşleme *(x, y) 
  {
    her i := 0; i < öz.yığın.boyut; i++: 
      eğer öz.yığın.Nesneler[i]:  
        İşleme(öz.yığın.Nesneler[i]->ad, öz.yığın.Nesneler[i]->öz);
  }*/

  Çizelge *k'x,y  
  küresel sanal iş Ekle no d32, Ek y: *kök't32,şey  
  { 
    Kök              := yeni(%**Çizelge->Nesneler[1]);  
    Kök->ad           = no; 
    Kök->öz           = Ek; 
    sıra             := Sıra(&Çizelge, no); 
    Kök->Sıradaki     = Çizelge->Nesneler[sıra]; 
    Çizelge->Nesneler[sıra] = Kök;
    Çizelge->yığın.Ekle(Kök); 
    Çizelge->boyut++;
    eğer Çizelge->boyut > (Çizelge->hacim >> 1):
      Çizelge->Yenile();
    dön Kök;
  }

  iş KökYazdır no d32, veri şey 
  {
    stdio::printf('--> %u, %s\n', no, veri);
  }

  

  iş _qs_bol Kök **kök't32,şey, dip t32, tepe t32: t32 
  {
    kesit := Kök[tepe]->ad; 
    i     := dip - 1; 
    her j := dip; j <= tepe -1; j++: 
      eğer Kök[j]->ad < kesit: 
      {
        i++; 
        &Kök[i] <=> &Kök[j]; 
      }
    &Kök[i+1] <=> &Kök[tepe]; 
    dön i +1;

  }

  küresel iş quickSort Girdi **kök't32, şey, dip t32, tepe t32 
  {
    eğer dip < tepe: 
    {
      kesit := _qs_bol(Girdi, dip, tepe); 
      quickSort(Girdi, dip, kesit -1); 
      quickSort(Girdi, kesit + 1, tepe);
    }
  }

  Çizelge *k'x,y  
  sanal küresel iş Sırala 
  {
    quickSort(Çizelge->yığın.Nesneler, 0, Çizelge->yığın.boyut-1);
  }


  iş Deneme 
  {
   // j := KökYazdır;
   /* değer i k't32,*t8 = {}; 
    i.Yapılandır(16);  
    değer metinler t8[32, 16] = {}; 
    her t := 1_sd32; t < 17; t++: 
    {
      _ad   := metinler[t-1]; 
      stdio::snprintf(_ad, 32, 'oz_%u', t); 
      n := 18 - t;
      Gelen := i.Ekle(n, _ad);  
      M := i.Ara(n); 
      stdio::printf('--> %d:%d [%u:%s] :%s:%p\n', 
        i.hacim, i.yığın.boyut, 
        Gelen->ad, Gelen->öz, M, Gelen->Sıradaki);
    }
    i.Sırala();
    i.Gez(KökYazdır);
    i.Temizle();*/
  }
}