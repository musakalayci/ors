dahili örs::merkez::c::stdio;
dahili örs::merkez::iletişim;


tür avlHücre 
{
  imge tam;
  _sıra tam;
  Sol *avlHücre;
  Sağ *avlHücre;
}

 iş büyük a tam, b tam  : tam
{
  eğer a > b: dön a;
  değilse: dön b;
}

Hücre *avlHücre 
iş sıra : tam 
{
  eğer Hücre:
    dön Hücre->_sıra;
  dön 0;
}

H *avlHücre 
bağımsız küresel iş Yeni imge tam : *avlHücre 
{
  Hücre := temiz(avlHücre);
  Hücre->_sıra = 1;
  Hücre->imge = imge;
  dön Hücre;
}

Hücre *avlHücre 
iş sağaDön : *avlHücre
{
  X := Hücre->Sol;
  T2 := X->Sağ;
  X->Sağ = Hücre;
  Hücre->Sol = T2;
  Hücre->_sıra = büyük(Hücre->Sol->sıra(), Hücre->Sağ->sıra()) + 1;
  X->_sıra    = büyük(X->Sol->sıra(), X->Sağ->sıra()) + 1;
  dön X;
}

Hücre *avlHücre 
iş solaDön :*avlHücre
{
  Y           := Hücre->Sağ;
  T2          := Y->Sol;
  Y->Sol       = Hücre;
  Hücre->Sağ   = T2;
  Hücre->_sıra = büyük(Hücre->Sol->sıra(), Hücre->Sağ->sıra()) + 1;
  Y->_sıra     = büyük(Y->Sol->sıra(), Y->Sağ->sıra()) + 1;
  dön Y; 
}

Hücre *avlHücre 
iş denge : tam 
{
  eğer Hücre:
  {
    dön Hücre->Sol->sıra() - Hücre->Sağ->sıra();
  }
  dön 0;
}

tür avl
{
  nüfus tam;
  Kök   *avlHücre;
}

A *avl 
bağımsız küresel iş Yeni : *avl
{
  Avl := temiz(avl);
  dön Avl;
}

Hücre *avlHücre
iş ekle imge tam : *avlHücre 
{
  eğer !Hücre: 
  {
    dön avlHücre::Yeni(imge);
  }
  eğer imge < Hücre->imge:
    Hücre->Sol = Hücre->Sol->ekle(imge);
  eğer ki imge > Hücre->imge:
    Hücre->Sağ = Hücre->Sağ->ekle(imge);
  değilse:
    dön Hücre;
  
  Hücre->_sıra = 1 + büyük(Hücre->Sol->sıra(), Hücre->Sağ->sıra());
  denge := Hücre->denge();
  eğer denge > 1 && (imge < Hücre->Sol->imge):
    dön Hücre->sağaDön();
  eğer denge < -1 && (imge > Hücre->Sağ->imge):
    dön Hücre->solaDön();
  eğer denge > 1 && (imge > Hücre->Sol->imge):
  {
    Hücre->Sol = Hücre->Sol->solaDön();
    dön Hücre->sağaDön();
  }
  eğer denge < -1 && (imge < Hücre->Sağ->imge):
  {
    Hücre->Sağ = Hücre->Sağ->sağaDön();
    dön Hücre->solaDön();
  }
  dön Hücre;
}

Avl *avl 
küresel iş Ekle imge tam: *avlHücre 
{
  A := Avl;
  Avl->Kök = Avl->Kök->ekle(imge);
  Avl->nüfus++;
  dön Avl->Kök;
}

 
küresel iş Gez Hücre *avlHücre
{
   eğer Hücre:
   {
     Gez(Hücre->Sol);
     stdio::printf('%d ', Hücre->imge);
     Gez(Hücre->Sağ);
   }
}


küresel iş 
YayvanÖrnek 
{
  değer tamlar tam[7] = [32, 15, 40, 12, 14, 59, 89]; 
  //değer ilk tam = tamlar[0];
  Kök := yayvan::Yeni();

  her i := 0; i < 7; i++:
  {
    değer eklenen tam = tamlar[i];
    Gelen := Kök->Ekle(tamlar[i]);
    stdio::printf('eklenen :%d, kök: %d, %d\n', eklenen, Gelen->imge, Kök->Kök->imge);
  }
  Yaz(Kök->Kök);
  iletişim::YeniSatır();
  Gelen := Kök->Ara(14);
  Yaz(Kök->Kök); 
  iletişim::YeniSatır();
  Yaz(Gelen);

  stdio::printf('--> Gelen: %d, Gelen: %p, Kök : %p\n\n\nAvl örnek:\n', 
    Gelen->imge, 
    Gelen, Kök->Kök);

  Avl := avl::Yeni();
  ilk := Avl->Kök;
  her i := 0; i < 7; i++: 
  {
    G := Avl->Ekle(tamlar[i]); 
    eğer i == 0:
    {
      ilk = G;
    }
  }
  Gez(Avl->Kök);
}
