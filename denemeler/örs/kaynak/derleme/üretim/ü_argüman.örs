dahili örs::merkez::bellek; 
dahili örs::merkez::c::str; 
dahili örs::derleme::imge::cins;
dahili örs::merkez::iletişim: i;

Argüman *argüman 
küresel iş Yapılandır
{
  str::memset(&Argüman->yıldız[0], 42, 32);
  Argüman->özet.ilk.Sıfırla();
  Argüman->özet.ikinci.Sıfırla();
  Argüman->özet.üçüncü.Sıfırla();
  Argüman->değişken.ilk.Sıfırla();
  Argüman->değişken.ikinci.Sıfırla();
  Argüman->değişken.üçüncü.Sıfırla();
}

Üretim *t 
küresel iş TürdenArgümana Nesne *nesne::t, Bellek *bellek::t: *bellek::t 
{
  Bellek->Sıfırla();
  Özet := Nesne->Özet;
  eğer !Özet: 
  {
    Bellek->Yaz('ptr null');
    dön Bellek;
  }
  eğer Nesne->Derece() < 0: 
  {
    Bellek->Yaz('void');
    dön Bellek;
  }
  Metin := Özet->Öz->nesne.Metin; 
  değer _çizelge *t8[2] = ['', '%']; 
  yüzde := hayır;
  eğer !Özet->Dizi: 
  {
    durum Özet->Gösterge->özellik: 
    {
      seçim imge::Özellik::Tür: 
        Tür := Özet->Gösterge->içerik.Tür;
        durum Tür->özellik.türü: 
        {
          seçim cins::Özellik::DonatılmışYalın: 
            yüzde = evet;  
          seçim cins::Özellik::Yalın: 
            Metin = Tür->Öz->nesne.Özet->Öz->nesne.Metin;
        } 
    }
  }
  eğer !Metin->boyut: 
  {
    //i::Yaz("ne yazılıyor: -%s-- %d\n", Metin->_harfler, Metin->boyut);
  }
  Bellek->Yaz('%s', Metin->_harfler); 
  eğer Nesne->Derece() >= 0: 
    Bellek->Yaz('%.*s', 
    Nesne->Derece(), Üretim->Argüman->yıldız); 
  dön Bellek;
}

Üretim *t 
küresel sanal iş TürdenİlkArgümana Nesne *nesne::t : *bellek::t 
{
  dön Üretim->TürdenArgümana(Nesne, &Üretim->Argüman->özet.ilk);
}


Üretim *t 
küresel sanal iş TürdenİkinciArgümana Nesne *nesne::t : *bellek::t 
{
  dön Üretim->TürdenArgümana(Nesne, &Üretim->Argüman->özet.ikinci);
}

Üretim *t 
küresel sanal iş TürdenÜçüncüArgümana Nesne *nesne::t : *bellek::t 
{
  dön Üretim->TürdenArgümana(Nesne, &Üretim->Argüman->özet.üçüncü);
}

Üretim *t 
küresel iş Arg Nesne *nesne::t, ABellek *bellek::t, ÖzetBelleği *bellek::t : *bellek::t 
{
  ABellek->Sıfırla();
  ÖzetBelleği->Sıfırla();
  eğer Nesne->no < 0: 
  {
    eğer !Nesne->Atıf: 
    {
      dön bildiri::Nesne(
          Üretim->Bölüm->Kaynak, 
          bildiri::Üretim::Genel, 
          &Nesne->Öz->konum,
          "Argüman hatası.");
    }
    durum Nesne->Atıf->özellik: 
    {
      seçim imge::Özellik::Boş: 
        Üretim->TürdenArgümana(Nesne, ÖzetBelleği); 
        eğer Nesne->Derece(): 
        {
          ABellek->Yaz('%s null', 
          ÖzetBelleği->_veri);
        }
        değilse:
        {
          eğer Nesne->Özet->YapıtaşıMı():
            ABellek->Yaz('%s 0', 
              ÖzetBelleği->_veri);
          değilse: 
          {
            bildiri::Nesne(
              Üretim->Bölüm->Kaynak, 
              bildiri::Üretim::Bulunamadı, 
              &Nesne->Öz->konum,
              "sorunlu"); 
          }
        } 
      seçim imge::Özellik::Hazne, imge::Özellik::Dizi: 
        Üretim->TürdenArgümana(Nesne, ÖzetBelleği);
        ABellek->Yaz('%s %s', 
          ÖzetBelleği->_veri, 
          Nesne->Metin->_harfler);
      seçim imge::Özellik::Sayı: 
        Üretim->TürdenArgümana(Nesne, ÖzetBelleği);
        Nesne->Atıf->içerik.sayı.Çıktı(
          Nesne->Özet->Gösterge->içerik.Tür->no,
          &Üretim->Bellekler->ilk);
        ABellek->Yaz('%s %s', 
          ÖzetBelleği->_veri, 
          Üretim->Bellekler->ilk._veri);
      seçim imge::Özellik::Harfler:  
        boyut := Nesne->Boyut->Öz->içerik.sabitSayı;
        
        ABellek->Yaz('i8* getelementptr inbounds ([%lu x i8], [%lu x i8]* @%s, i64 0, i64 0)', 
          boyut, boyut, 
          Nesne->Atıf->Ad->_harfler);
        Üretim->Bölüm->DeğerlereEkle(Nesne->Atıf); 
        //i::Yaz("harfler: %s\n", ABellek->_veri);  
      seçim imge::Özellik::BirimDeğeri: 
        Üretim->TürdenArgümana(Nesne, ÖzetBelleği); 
        ABellek->Yaz('%s @%s', 
          ÖzetBelleği->_veri, 
          Nesne->Metin->_harfler);
      seçim imge::Özellik::İşlem, 
            imge::Özellik::Türİşlemi,
            imge::Özellik::Bünyeİşlem, 
            imge::Özellik::İşlemTanımı: 
        Üretim->TürdenArgümana(Nesne, ÖzetBelleği); 
        ABellek->Yaz('%s @%s', 
          ÖzetBelleği->_veri, 
          Nesne->Metin->_harfler);
      seçim imge::Özellik::Metin: 
        Üretim->Bölüm->DeğerlereEkle(Nesne->Atıf); 
        ABellek->Yaz(
          '%%metin* getelementptr inbounds '
          '(%%metin, %%metin* @%s, i32 0)', 
          Nesne->Atıf->Ad->_harfler);
      seçim imge::Özellik::SabitSayı: 
        Üretim->TürdenArgümana(Nesne, ÖzetBelleği);
        ABellek->Yaz('%s %lu', 
          ÖzetBelleği->_veri, 
          Nesne->Atıf->içerik.sabitSayı);
    }
  }
  değilse: 
  {
    Üretim->TürdenArgümana(Nesne, ÖzetBelleği);
    Ad := &Üretim->Bellekler->ad;
    Ad->Sıfırla(); 
    durum Nesne->Kök(): 
    {
      seçim nesne::KökDeğer::Küresel,
            nesne::KökDeğer::Dış: 
        Ad->Yaz(' @%s', Nesne->Metin->_harfler);
      varsayılan: 
        Ad->Yaz(' %%%d', Nesne->no);
    }
    ABellek->Yaz('%s%s', ÖzetBelleği->_veri, Ad->_veri);
  }
  dön ABellek;
}

Üretim *t 
küresel sanal iş İlkArgüman Nesne *nesne::t : *bellek::t 
{
  dön Üretim->Arg(Nesne, &Üretim->Argüman->değişken.ilk, &Üretim->Argüman->özet.ilk);
}

Üretim *t 
küresel sanal iş İkinciArgüman Nesne *nesne::t : *bellek::t 
{
  dön Üretim->Arg(Nesne, 
    &Üretim->Argüman->değişken.ikinci, &Üretim->Argüman->özet.ikinci);
}

Üretim *t 
küresel sanal iş ÜçünüArgüman Nesne *nesne::t : *bellek::t 
{
  dön Üretim->Arg(Nesne, 
    &Üretim->Argüman->değişken.üçüncü, &Üretim->Argüman->özet.üçüncü);
}


Üretim *t 
küresel iş YalınArgüman Nesne *nesne::t, Argüman *bellek::t : *bellek::t 
{
  Argüman->Sıfırla();
  eğer Nesne->no < 0: 
  {
    durum Nesne->Atıf->özellik: 
    {
      seçim imge::Özellik::Boş: 
      seçim imge::Özellik::Sayı:
        Nesne->Atıf->içerik.sayı.Çıktı(
          Nesne->Özet->Gösterge->içerik.Tür->no,
          &Üretim->Bellekler->ilk);
        Argüman->Yaz('%s', 
          Üretim->Bellekler->ilk._veri); 
      seçim imge::Özellik::SabitSayı: 
        Argüman->Yaz('%ld', Nesne->Atıf->içerik.sabitSayı);
      seçim imge::Özellik::Harfler: 
      seçim imge::Özellik::Metin: 
      varsayılan:
    }
  }
  değilse: 
  {
    durum Nesne->Kök(): 
    {
      seçim nesne::KökDeğer::Küresel,
            nesne::KökDeğer::Dış: 
        Argüman->Yaz(' @%s', Nesne->Metin->_harfler); 
      varsayılan: 
        Argüman->Yaz(' %%%d', Nesne->no);
    }
  }
  dön Argüman;
}

Üretim *t 
küresel sanal iş İkinciYalın Nesne *nesne::t : *bellek::t 
{
  dön Üretim->YalınArgüman(Nesne, 
    &Üretim->Argüman->değişken.ikinci);
}

Üretim *t 
küresel sanal iş ÜçüncüYalın Nesne *nesne::t : *bellek::t 
{
  dön Üretim->YalınArgüman(Nesne, 
    &Üretim->Argüman->değişken.üçüncü);
}

Üretim *t 
küresel sanal iş İlkYalın Nesne *nesne::t : *bellek::t 
{
  dön Üretim->YalınArgüman(Nesne, 
    &Üretim->Argüman->değişken.ilk);
}