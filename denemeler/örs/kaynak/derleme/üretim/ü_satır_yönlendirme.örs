dahili örs::derleme::çözümleme::simge;

Üretim *t 
iş içGit Git *imge::kesit::içGit: *nesne::t 
{
  Son := Üretim->yığın->SonNesne->Atıf; 
  eğer Son: 
  {
    durum Son->özellik: 
    {
      seçim imge::Özellik::Geç,
            imge::Özellik::Dön, 
            imge::Özellik::Devam,
            imge::Özellik::I_Geçiş,
            imge::Özellik::Son,
            imge::Özellik::I_Git, 
            imge::Özellik::I_KoşulluGit,
            imge::Özellik::Tekrar: 
        varsayılan: 
          Üretim->Bölüm->Genel()->Yaz("  br label %%%s\n",
            Git->Konum->Öz->Ad->_harfler);
    }
  }
  değilse: 
  {
    i::Yaz("");
    //Üretim->Derleme->Döküm->İmge(Üretim->yığın->SonNesne->Öz, 0, '\n');
  }
  
  dön &Git->Öz->nesne;
}

Üretim *t 
iş içDönüş İmge *imge::t: *nesne::t 
{
  
  Çıktı := Üretim->Sonİşlem->Çıktı;
  Özet  := Çıktı->Özet; 
  durum Özet->Gösterge->içerik.Tür->no: 
  {
    seçim simge::Terim::Boş: 
      Üretim->Bölüm->Genel()->Yaz("  ret void\n");  
    varsayılan: 
      Gelen := Çıktı->Öz->nesne.Yükle(Üretim);
      Arg := Üretim->İlkArgüman(Gelen);  
      Üretim->Bölüm->Genel()->Yaz("  ret %s\n", Arg->_veri); 
  }
  dön &İmge->nesne;
}

Üretim *t 
iş içKoşulluGit Gidiş *imge::kesit::koşulluGit: *nesne::t 
{
  Koşul := Üretim->ifade(Gidiş->Koşul, evet);
  eğer !Koşul: dön boş; 

  eğer !Koşul->Özet: dön boş; 

  Karşılaştırma := Koşul->SıfıraKarşılaştırma(Üretim);

  Üretim->Bölüm->Genel()->Yaz("  br i1 %%%d, label %%%s, label %%%s\n",
    Karşılaştırma->no, 
    Gidiş->Evet->Öz->Ad->_harfler, 
    Gidiş->Hayır->Öz->Ad->_harfler);  
  dön &Koşul->Öz->nesne;
}

Üretim *t 
küresel iş _son İmge *imge::t : *nesne::t 
{
  SonDöngü := Üretim->yığın.döngüSonları.Son();
  eğer !SonDöngü: 
  {
    dön bildiri::Nesne(
          Üretim->Bölüm->Kaynak, 
          bildiri::Üretim::Bulunamadı, 
          &İmge->konum,
          "'son' satırı döngü dışında kullanılamaz.");
  }
  Gidiş := imge::kesit::YeniİçGit(Üretim->Bölüm->Hafıza, SonDöngü, imge::Özellik::I_Git); 
  dön Üretim->satır(Gidiş->Öz);
}

Üretim *t 
küresel iş _devam İmge *imge::t : *nesne::t 
{
  SonKoşul := Üretim->yığın.döngüKoşulları.Son();
  eğer !SonKoşul: 
  {
    dön bildiri::Nesne(
          Üretim->Bölüm->Kaynak, 
          bildiri::Üretim::Bulunamadı, 
          &İmge->konum,
          "'devam' satırı döngü dışında kullanılamaz.");
  }
  Gidiş := imge::kesit::YeniİçGit(
    Üretim->Bölüm->Hafıza, SonKoşul, imge::Özellik::I_Git); 
  dön Üretim->satır(Gidiş->Öz);
}

Üretim *t 
küresel iş _tekrar İmge *imge::t : *nesne::t 
{
  SonDurum := Üretim->yığın.durumlar.Son();

  eğer !SonDurum: 
  {
    dön bildiri::Nesne(
          Üretim->Bölüm->Kaynak, 
          bildiri::Üretim::Bulunamadı, 
          &İmge->konum,
          "'tekrar' satırı durum satırları dışında kullanılamaz.");
  }
  Gidiş := imge::kesit::YeniİçGit(
    Üretim->Bölüm->Hafıza, SonDurum, imge::Özellik::I_Git); 
  dön Üretim->satır(Gidiş->Öz);
}


Üretim *t 
küresel iş _geç İmge *imge::t : *nesne::t 
{
  SonDurum := Üretim->yığın.seçimler.Son();

  eğer !SonDurum: 
  {
    dön bildiri::Nesne(
          Üretim->Bölüm->Kaynak, 
          bildiri::Üretim::Bulunamadı, 
          &İmge->konum,
          "'tekrar' satırı durum satırları dışında kullanılamaz.");
  }
  Gidiş := imge::kesit::YeniİçGit(
    Üretim->Bölüm->Hafıza, SonDurum, imge::Özellik::I_Git); 
  dön Üretim->satır(Gidiş->Öz);
}

Üretim *t 
iş _dön İmge *imge::t : *nesne::t 
{
  değer Sanal *imge::kesit::t = Üretim->yığın.sanalSonları.Son();  
  değer Gelen *nesne::t       = boş;
  değer Arg   *bellek::t      = boş;
  eğer Sanal: 
  {
    Gelen = Üretim->ifade(İmge->içerik.Dön, hayır); 
    Üretim->Bölüm->Genel()->Yaz("; sanal dönüş\n"); 
    eğer !Gelen: 
      dön boş; 
    durum Gelen->Atıf->özellik: 
    {
      seçim imge::Özellik::Hata: 
        dön Gelen;
    }

    durum Gelen->özellik.makina: 
    {
      seçim nesne::Makina::Çağrı, 
            nesne::Makina::KonumAlma: 
      varsayılan: 
        durum Gelen->Atıf->özellik: 
        {
          seçim imge::Özellik::SanalAtıf: 
          varsayılan: 
            Gelen = Gelen->Yükle(Üretim); 
        }
    }
    Gelen->İşlemlendir(nesne::Makina::SanalDön);
    //sonuça geçirip sona atlıyoruz işte ya. 
    NesneKökü := &Sanal->Kök->nesne;
    NesneKökü = NesneKökü->Geçir(Üretim, Gelen);
    Bağ := imge::kesit::YeniİçGit(Üretim->Bölüm->Hafıza, Sanal, imge::Özellik::I_Git);
    dön Üretim->satır(Bağ->Öz); 
  }
  değilse: 
  {
    Gelen = Üretim->ifade(İmge->içerik.Dön, hayır); 
    eğer !Gelen: 
    {
      i::Acil("noluyor len");
      dön &İmge->nesne;
    }
    durum Gelen->Atıf->özellik: 
    {
      seçim imge::Özellik::İfadeSonu: 
        Üretim->Bölüm->Genel()->Yaz("  ret void\n"); 
      seçim imge::Özellik::Boş: 
        Özet := Üretim->Sonİşlem->Çıktı->Özet;
        Arg = Üretim->TürdenİlkArgümana(
          &Özet->Öz->nesne); 

        eğer Özet->Öz->nesne.Derece():
          Üretim->Bölüm->Genel()->Yaz("  ret %s null; %d\n", Arg->_veri, Özet->Öz->nesne.Derece());
        değilse: 
        {
          eğer Özet->YapıtaşıMı(): 
          {
            Üretim->Bölüm->Genel()->Yaz("  ret %s 0\n", Arg->_veri);
          }
          değilse: 
            dön bildiri::Nesne(
              Üretim->Bölüm->Kaynak, 
              bildiri::Üretim::Genel, 
              &İmge->konum,
              "Sorunlu dönüş satırı.");
        }  
      varsayılan: 
        durum Gelen->Makina(): 
        {
          seçim 
                nesne::Makina::SanalÇağrı: 
            //i::Yaz("Gelen _ %p\n", Gelen);
          seçim nesne::Makina::Çağrı,
                nesne::Makina::İçSabit, 
                nesne::Makina::KonumAlma, 
                nesne::Makina::ÇeviriYapıtaşı,
                nesne::Makina::Karşılaştırma:
          varsayılan: 
            Gelen = Gelen->Yükle(Üretim);
            
        }
        Arg = Üretim->İlkArgüman(Gelen);  
        Üretim->Bölüm->Genel()->Yaz("  ret %s\n", Arg->_veri); 
    }
  }
  dön &İmge->nesne;
}