

Üretim *t 
küresel iş Tanımlanan Aranan *imge::t: *imge::t 
{
  değer Ad *metin = boş;
  durum Aranan->özellik: 
  {
    seçim imge::Özellik::Saf: 
      Ad = Aranan->Ad;
    seçim imge::Özellik::Çağrı:
      Ad = Aranan->Ad;
    varsayılan: 
      dön boş;
  }
  değer Bulunan *imge::t = boş; 
  değer Gelen   *imge::t = boş; 
  SanalDağarcık := Üretim->yığın.dağarcıklar.Son(); 
  değer Dağarcık *imge::dağarcık::t = Aranan->Dağarcık; 
  
  eğer SanalDağarcık: 
  {
    Gelen = SanalDağarcık->Ara(Ad); 
    eğer Gelen: 
    {
      durum Gelen->özellik: 
      {
        seçim imge::Özellik::SanalAtıf: 
          dön Gelen;
      }
    }
  }
  her Dağarcık: 
  {
    Gelen = boş;
    Gelen = Dağarcık->Ara(Ad); 
    eğer Gelen: 
    {
      durum Gelen->özellik: 
      {
        seçim imge::Özellik::Değer, 
              imge::Özellik::Paskal: 
          Aranan->nesne.Özet = Gelen->içerik.Değer->Özet;
          Aranan->nesne.Atıf = Gelen; 
          Bulunan = Gelen; 
        seçim imge::Özellik::Değişken: 
          Aranan->nesne.Özet = Gelen->içerik.Değişken->Özet;
          Aranan->nesne.Atıf = Gelen; 
          Bulunan = Gelen; 
          son;
      }
    }
    Dağarcık = Dağarcık->Üst;
  }
  eğer !Bulunan: 
    Bulunan = Üretim->Arama(Aranan);
  eğer Bulunan: 
  {
    durum Aranan->özellik: 
    {
      seçim imge::Özellik::Çağrı:
        /*Aranan->nesne.AtıfsızNakil(&Bulunan->nesne);
        Aranan->nesne.Atıf = Bulunan;
        Aranan->içerik.Çağrı->Atıf = Bulunan;*/
        dön Bulunan;
      seçim imge::Özellik::Saf: 
        Aranan->içerik.İfade = Bulunan; 
        Aranan->nesne.Nakil(&Bulunan->nesne);
        Aranan->nesne.Atıf = Bulunan;
        dön Aranan;
    }
  }
  dön Bulunan;
} 

Üretim *t 
küresel iş _sonuç İmge *imge::t, yükleme tam: *nesne::t 
{
  değer SanalDağarcık *imge::dağarcık::t = Üretim->yığın.dağarcıklar.Son(); 
  
  eğer SanalDağarcık: 
  {
    i::Yaz("imekalyuikealkileyayulikeayluimkealymkuiylemakyuliekalyike");
  }
  değilse: 
  {
    Çıktı := Üretim->Sonİşlem->Çıktı;  
    eğer Çıktı->Özet->HiçMi(): 
    {
      dön bildiri::Nesne(
          Üretim->Bölüm->Kaynak, 
          bildiri::Üretim::Bulunamadı, 
          &İmge->konum,
          "Boş sonuç olamaz.", İmge->Ad->_harfler);
    } 

    
    Nesne := &İmge->nesne;  
    
    Nesne->Nakil(&Çıktı->Öz->nesne);
    eğer !Çıktı->Özet->YapıtaşıMı():
    {
      eğer (Nesne->Derece() < 1): 
      {
        dön bildiri::Nesne(
          Üretim->Bölüm->Kaynak, 
          bildiri::Üretim::Bulunamadı, 
          &İmge->konum,
          "Derecesiz dönüşlerin sonuç ifadesi olamaz.");
      }
    }
    //i::Yaz("noooo: %d\n", Nesne->no); 
    Nesne->Anlamlandır(nesne::Anlam::Değer); 
    eğer yükleme: 
      dön Nesne->Yükle(Üretim);
    dön Nesne;
  }
}

Üretim *t 
küresel iş ifade İmge *imge::t, yükle tam: *nesne::t 
{
  //i::Yaz("ifadeeeeeeeeeee %d\n", İmge->özellik); 
  //Üretim->Derleme->Döküm->İmge(İmge, '', 0);
  eğer !İmge: dön boş;
  değer Gelen *nesne::t = boş; 
  durum İmge->özellik: 
  {
    seçim imge::Özellik::Boş, imge::Özellik::İfadeSonu: 
      dön &İmge->nesne;
    seçim imge::Özellik::İfade: 
      dön Üretim->ifade(İmge->içerik.İfade, yükle); 
      
    seçim imge::Özellik::Metin,
          imge::Özellik::Dizi, 
          imge::Özellik::Harfler,
          imge::Özellik::Harf: 
      dön &İmge->nesne;
    seçim imge::Özellik::SabitSayı,
          imge::Özellik::Sayı: 
      İmge->nesne.Öz = İmge; 
      İmge->nesne.Atıf = İmge; 
      İmge->nesne.İşlemlendir(nesne::Makina::Geç); 
      dön &İmge->nesne;
    seçim imge::Özellik::Hazne: 
      dön &İmge->nesne;
    seçim imge::Özellik::Atama: 
      dön Üretim->atama(İmge->içerik.Temel); 
    seçim imge::Özellik::Eşitlik:
      dön Üretim->atamaEşitle(İmge->içerik.Temel);
    seçim imge::Özellik::KonumAlma: 
      dön Üretim->konumAlma(İmge->içerik.Tekil);
    seçim imge::Özellik::KonumDeğeri: 
      dön Üretim->konumDeğeri(İmge->içerik.Tekil, yükle);
    seçim imge::Özellik::Arama, imge::Özellik::Çağrı: 
      dön Üretim->aramaİfadesi(İmge); 
    seçim imge::Özellik::TemelIşlem: 
      dön Üretim->temel(İmge->içerik.Temel);
    seçim imge::Özellik::Sonuç: 
      dön Üretim->_sonuç(İmge, yükle);
    seçim imge::Özellik::Saf: 
    {
      //i::Yaz("saftirik : %s\n", İmge->Ad->_harfler);
      Bulunan := Üretim->Tanımlanan(İmge);
      eğer !Bulunan: 
      {
        SS := Üretim->yığın.dağarcıklar.Son();
        //Üretim->Derleme->Döküm->İmge(SS->Öz, 0, '\n');
        dön bildiri::Nesne(
          Üretim->Bölüm->Kaynak, 
          bildiri::Üretim::Bulunamadı, 
          &İmge->konum,
          "kök ifade '%s' bulunamadı. %p", İmge->Ad->_harfler);
      }
      Atıf := Bulunan->nesne.Atıf; 
      eğer !Atıf: 
        dön bildiri::Nesne(
          Üretim->Bölüm->Kaynak, 
          bildiri::Üretim::Bulunamadı, 
          &İmge->konum,
          "Tanımlanan değer '%s' daha başlatılmadı.",
          İmge->Ad->_harfler);
      durum Atıf->özellik: 
      {
        seçim imge::Özellik::Değişken: 
          eğer Atıf->içerik.Değişken->özelleştirme & imge::değişken::Dto::Bağımsız: 
            dön bildiri::Nesne(
              Üretim->Bölüm->Kaynak, 
              bildiri::Üretim::Bulunamadı, 
              &İmge->konum,
              "Bağımsız tür işlemi değişkeni erişilemez.");
          Gelen = &Bulunan->nesne; 
        seçim imge::Özellik::SanalBirimDeğeri:
         // i::Acil("buradayız demi ? %p\n", Atıf->içerik.Değer->Başlatma->nesne.Atıf);
          dön &Atıf->nesne.Atıf->nesne;
        seçim imge::Özellik::BirimDeğeri:
        {
          durum Atıf->nesne.özellik.kök: 
          {
            seçim nesne::KökDeğer::Küresel,
                  nesne::KökDeğer::Dış: 
              Gelen = &Atıf->nesne;
          }
        }
        seçim imge::Özellik::İşlem,
              imge::Özellik::Türİşlemi,
              imge::Özellik::Bünyeİşlem, 
              imge::Özellik::İşlemTanımı:
          Nesne := nesne::Yeni(Üretim->Bölüm->Hafıza, Bulunan, nesne::Anlam::Değer); 
          Nesne->Nakil(&Bulunan->nesne);
          Nesne->İşlemlendir(nesne::Makina::Konumİşlem);
          Nesne->Yükselt();
          
          //i::Yaz("oldu mu la ? %d\n", Nesne->Atıf->özellik);
          dön Nesne;
        seçim imge::Özellik::SabitSayı,
              imge::Özellik::Sayı: 
          Atıf->nesne.İşlemlendir(nesne::Makina::Geç); 
          dön &Atıf->nesne;
        
        seçim imge::Özellik::SanalAtıf:

          Atıf->nesne.İşlemlendir(nesne::Makina::Geç);
          dön &Atıf->nesne;
        seçim imge::Özellik::Tür: 
          dön bildiri::Nesne(
            Üretim->Bölüm->Kaynak, 
            bildiri::Üretim::Bulunamadı, 
            &İmge->konum,
            "Hatalı ifade.");
        varsayılan: 
          Gelen = &Bulunan->nesne; 
      } 
      eğer yükle: 
      {
        dön Gelen->Yükle(Üretim);  
      }
    }
    seçim imge::Özellik::H_Yeni, 
          imge::Özellik::H_Temiz: 
      dön Üretim->_yeni(İmge);
    seçim imge::Özellik::Seç: 
      dön Üretim->_seç(İmge->içerik.Seç);
    seçim imge::Özellik::Değil: 
      dön Üretim->değil(İmge->içerik.Tekil);
    seçim imge::Özellik::Karşılaştırma: 
      dön Üretim->karşılaştırma(İmge->içerik.Temel);
    seçim imge::Özellik::TürAlma: 
      dön Üretim->İfadeTürü(İmge, yükle);
    seçim imge::Özellik::Boyut: 
      dön Üretim->ifadeBoyut(İmge);
    seçim imge::Özellik::DiziErişim: 
      dön Üretim->diziErişim(İmge->içerik.DiziErişim, yükle);
    seçim imge::Özellik::KonumErişim,
          imge::Özellik::TürErişim: 
      dön Üretim->erişim(İmge->içerik.Temel, yükle); 
    seçim imge::Özellik::Mantık:
      dön Üretim->mantık(İmge->içerik.Temel);
    seçim imge::Özellik::Çeviri: 
      dön Üretim->çeviri(İmge, yükle);
    seçim imge::Özellik::Arkaİşlem:
      dön Üretim->arka(İmge->içerik.Tekil); 
    seçim imge::Özellik::TekilArama: 
      dön Üretim->tekilArama(İmge, yükle); 
    seçim imge::Özellik::H_Yenile: 
      dön Üretim->_yenile(İmge); 
    seçim imge::Özellik::H_Geçir: 
      dön Üretim->HafızaGeçir(İmge);
    seçim imge::Özellik::Geçir: 
      dön Üretim->İfadeGeçir(İmge);
    seçim imge::Özellik::Değiştir: 
      dön Üretim->değiştir(İmge->içerik.Temel);
    varsayılan: 
      dön bildiri::Nesne(
          Üretim->Bölüm->Kaynak, 
          bildiri::Üretim::Genel, 
          &İmge->konum,
          "sorunlu ifade. %d", İmge->özellik);
  }
  dön Gelen;
}

