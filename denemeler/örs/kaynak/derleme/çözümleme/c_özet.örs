
Çözümleme *t 
sanal iş TürÖzetiKonum TürÖzeti *imge::cins::özet 
{
  Simge := Çözümleme->şuanki();
  
  durum Simge->özellik: 
  {
    seçim simge::Özellik::Yıldız:
      TürÖzeti->derece++; 
      Simge = Çözümleme->tekil();
      tekrar;       
  } 
}


Çözümleme *t 
iş türÖzetiÖnifade : *imge::t
{
  değer İmge *imge::t = boş; 
  Simge := Çözümleme->şuanki();
  durum Simge->özellik: 
  {
    seçim simge::Özellik::Sözcük:
      İmge = imge::Adlı(
        Çözümleme->hafıza(),
        Simge->içerik.Metin, 
        imge::Özellik::Saf);
      İmge->KonumGüncelle(Simge); 
      Çözümleme->kütüphaneGüncelle(İmge);
    seçim simge::Terim::yapıtaşları:
      İmge = Çözümleme->Derleme->Yapıtaşı(Simge->özellik)->Öz; 
    seçim simge::Özellik::Arama,
          simge::Özellik::Nokta, 
          simge::Özellik::İleri_Ok,
          simge::Özellik::Yüzde, 
          simge::Özellik::T_Ve:
      İmge = imge::YeniNoktalama(
        Çözümleme->hafıza(),
        Çözümleme->şuanki()); 
      Çözümleme->kütüphaneGüncelle(İmge);
    varsayılan: 
     dön Çözümleme->İfadeSonu;
  }
  Çözümleme->tekil(); 
  dön İmge;
}

Çözümleme *t 
iş türİfadesi enAz tam: *imge::t 
{
  değer Şuanki   *simge::t  = boş;
  değer Gelen    *imge::t   = boş;
  değer gelecekİmgeTürü tam = imge::Özellik::Boş;
  değer öncelik         işlemÖnceliği = {};
  Hafıza := Çözümleme->hafıza();
  Solİfade := Çözümleme->türÖzetiÖnifade();
  durum Solİfade->özellik: 
  {
    seçim imge::Özellik::Saf:
    seçim imge::Özellik::Tür:
    seçim imge::Özellik::Noktalama:
      noktalama := Solİfade->içerik.noktalama;
      İşlem := imge::tekil::Yeni(Hafıza, Solİfade, imge::Özellik::Önİşlem);
      İşlem->özellik = noktalama;
      öncelik.türİfadesiÖnifadeÖnceliği(noktalama);
      Sağİfade := Çözümleme->türİfadesi(öncelik.sağ);
      İşlem->Değer = Sağİfade;
      durum noktalama:
      {
        seçim simge::Özellik::Yüzde:
          Solİfade->özellik = imge::Özellik::TürAlma;
        seçim simge::Özellik::Yıldız:
          Solİfade->özellik = imge::Özellik::KonumDeğeri;
        seçim simge::Özellik::T_Ve:
          Solİfade->özellik = imge::Özellik::KonumAlma;
        varsayılan:
          dön Çözümleme->Hataİmla(
                "Tür özeti için tanımlı olmayan ifade.");
      }
    seçim imge::Özellik::İfadeSonu:
      dön Solİfade;
    varsayılan:
      dön Çözümleme->Hataİmla("Sorunlu tür ifadesi.");
  }
  her Çözümleme->Devam():
  {
    Şuanki = Çözümleme->şuanki();
    Gelen  = Çözümleme->türÖzetiÖnifade();
    gelecekİmgeTürü = imge::Özellik::Boş; 
    durum Gelen->özellik:
    {
      seçim imge::Özellik::Saf:
        dön Solİfade;
      seçim imge::Özellik::Noktalama:
        gelecekİmgeTürü = Gelen->içerik.noktalama;
      seçim imge::Özellik::İfadeSonu:
        dön Solİfade;
      seçim imge::Özellik::Bildiri,
            imge::Özellik::Hata:
        dön Gelen;
      varsayılan:
        stdio::printf('-> Gelen : %d', Gelen->özellik);
        Çözümleme->Derleme->Döküm->İmge(Gelen, 0, '\n');
        dön Çözümleme->Hataİmla("Tür özeti için istenmeyen imge.");
    }
    öncelik.türİfadesiOrtaifadeÖnceliği(gelecekİmgeTürü);
    eğer öncelik.sol:
    {
      eğer öncelik.sol < enAz:
        son;
      Temel         := imge::temel::Yeni(Hafıza, Gelen);
      Gelen->konum   = Solİfade->konum;
      Sağİfade      := Çözümleme->türİfadesi(öncelik.sağ);
      Temel->Sol     = Solİfade;
      Temel->Sağ     = Sağİfade;
      Temel->özellik = gelecekİmgeTürü; 
      Solİfade       = Gelen;
      durum gelecekİmgeTürü:
      {
        seçim simge::Özellik::İleri_Ok:
          Gelen->özellik = imge::Özellik::KonumErişim;
        seçim simge::Özellik::Nokta:
          Gelen->özellik = imge::Özellik::TürErişim;
        seçim simge::Özellik::Arama:
          Gelen->özellik = imge::Özellik::Arama;
        varsayılan:
          
          dön Çözümleme->Hataİmla(
            "İkiz ifade için istenmeyen simge."); 
      }
    }
  }
  dön Solİfade;
}

Çözümleme *t 
iş türÖzetiDizi 
  Özet *imge::cins::özet: 
  *imge::t 
{
  durum Çözümleme->şuanki()->özellik:
  {
    seçim simge::Özellik::Kutu_Aç:
      Çözümleme->tekil();
    varsayılan:
      dön Özet->Öz;
  }
  Seviye := Özet;
  Geçici := Özet; 
  Yeni := Özet;
  Hafıza := Çözümleme->hafıza(); 
  Yığın := &Çözümleme->yığınlar.imgeler;
  Yığın->Sıfırla(); 
  Yığın->Ekle(Özet->Öz);
  değer Boyut *imge::t = boş; 
  i := 1;
  her Çözümleme->Devam() && i < 16:
  {
    durum Çözümleme->şuanki()->özellik:
    {
      seçim simge::Özellik::Kutu_Kapa:
        Çözümleme->sıradaki();
        son;
      seçim simge::Özellik::Virgül:
        Çözümleme->sıradaki();
      varsayılan:
      {
        Boyut = Çözümleme->ifade(0); 
        
        //stdio::printf('nnnnnnnnnnnnnnnnnnnnnnnnnn %s\n', Çözümleme->Derleme->Döküm->Bellek->_veri);
        durum Boyut->özellik:
        {
          seçim imge::Özellik::Hata:
            dön Boyut;
        }
        Geçici                  = imge::cins::YeniÖzet(Hafıza, Seviye->Gösterge);
        Geçici->Öz->nesne.Boyut = &Boyut->nesne;
        Yığın->Ekle(Geçici->Öz);
        i++;

      }
    }
  }

  eğer i > 16:
    dön Çözümleme->HataSınır(
      "Tür özeti için dizi sınırı aşılmış.");
  //_imgeler.Temizle(); 
  boyut := Çözümleme->yığınlar.imgeler.boyut; 
 
  her i := 1; i < boyut; i++: 
  {
    eğer (i+1) == boyut: 
    {
      Yığın->Nesneler[i]->içerik.TürÖzeti->Dizi = Özet; 
      //stdio::printf('nnnnnnnnnnnnnnnnnnnnnnnnnn %d\n', i);
    }
    değilse: 
    {
      Seviye = Yığın->Nesneler[i]->içerik.TürÖzeti;
      // stdio::printf('nnnnnnnnnnnnnnnnnnnnnnnnnn %d\n', i);
      //stdio::printf('nnnnnnnnnnnnnnnnnnnnnnnnnn\n');
      Geçici = Yığın->Nesneler[i+1]->içerik.TürÖzeti; 
      Seviye->Dizi = Geçici;
    }
  }
  dön Yığın->Nesneler[1];
}

Çözümleme *t 
iş türÖzetiTaç 
  Özet *imge::cins::özet: 
  *imge::t 
{

  durum Çözümleme->şuanki()->özellik:
  {
    seçim simge::Özellik::Tek_Tırnak:
    varsayılan:
      dön Özet->Öz;
  }

      //Çözümleme->şuanki()->Yaz("taç");
  Taç := Özet->YeniTaç(Çözümleme->hafıza()); 
  değer Donatım *imge::cins::özet = boş;
  Donatım = <**imge::cins::özet>Taç->_donatım;
  değer Ast *imge::cins::özet = boş;
  değer Gelen *imge::t = boş;
  Çözümleme->tekil();
  her i := 0; i < 2 && Çözümleme->Devam(); : 
  {
    durum Çözümleme->şuanki()->özellik:
    {
      seçim simge::Özellik::Virgül:
        Çözümleme->tekil(); 
      seçim simge::Özellik::Noktalı_Virgül: 
        son;
      varsayılan:
        Ast = imge::cins::YeniÖzet(Çözümleme->hafıza(), boş);
        Gelen = Çözümleme->türÖzeti(Ast);
        durum Gelen->özellik: 
        {
          seçim imge::Özellik::Hata:
            dön Gelen;
          seçim imge::Özellik::TürÖzeti:
            Taç->_donatım[i] = Gelen->içerik.TürÖzeti;

            //stdio::printf('--> boyut %d; Gelen: %p, hedef: %p\n', Taç->boyut, Gelen->içerik.TürÖzeti, Taç->_donatım[i]);
            //Çözümleme->Derleme->Döküm->İmge(Gelen, 0, '\n');
            Taç->boyut++;
            i++;
          varsayılan:
            dön Çözümleme->Beklenmeyenİmge(
              Gelen, "Tür özeti için beklenmeyen imge.");
        }
    }
  }
  dön Özet->Öz;
}

Çözümleme *t 
iş türÖzeti Özet *imge::cins::özet: *imge::t 
{
  Şuanki := Çözümleme->şuanki(); 
  //Şuanki->Yaz("Tür özeti: ");
  Özet->Öz->KonumGüncelle(Çözümleme->şuanki());
  durum Şuanki->özellik:
  {
    seçim simge::Özellik::Eşittir,
          simge::Özellik::Virgül, 
          simge::Özellik::Noktalı_Virgül:
      dön Çözümleme->Hataİmla("Tür ifadesi bekleniyor.");
    seçim simge::Özellik::Üç_Nokta: 
      Özet->Gösterge = Çözümleme->Derleme->İmgeler->Değişken;
      Çözümleme->tekil();
      dön Özet->Öz;
  }
  
  Çözümleme->TürÖzetiKonum(Özet); 
  Gelen := Çözümleme->türİfadesi(0);
  durum Gelen->özellik:
  {
    seçim imge::Özellik::Hata,
          imge::Özellik::Bildiri:
      dön Gelen;
    varsayılan:
      Özet->Gösterge = Gelen;
  }
  Gelen = Çözümleme->türÖzetiTaç(Özet);  

  durum Gelen->özellik:
  {
    seçim imge::Özellik::Hata:
      dön Gelen;
  }
  dön Çözümleme->türÖzetiDizi(Özet);
}


Çözümleme *t 
iş türÖzetiDiziEsnek 
  Özet *imge::cins::özet: 
  *imge::t 
{
  durum Çözümleme->şuanki()->özellik: 
  {
    seçim simge::Özellik::Kutu_Aç:
      Çözümleme->sıradaki(); 
    varsayılan:
      dön Özet->Öz;
  }
  değer Boyut *imge::t = boş;
  her Çözümleme->Devam():
  {
    durum Çözümleme->şuanki()->özellik:
    {
      seçim simge::Özellik::Kutu_Kapa:
        Çözümleme->sıradaki();
        son;
      seçim simge::Özellik::Virgül: 
        dön Çözümleme->Hataİmla(
          "Sadece tek boyutlu hafıza istenebilir.");
      varsayılan:
        Boyut = Çözümleme->ifade(0);
        eğer Boyut:
        {
          durum Boyut->özellik: 
          {
            seçim imge::Özellik::Hata:
              dön Boyut; 
            varsayılan:
              Özet->Öz->nesne.Atıf = Boyut;
          }
        }
        değilse:
          dön Çözümleme->Hataİmla("Sorunlu hafıza boyutu ifadesi");
    }
  }
  dön Özet->Öz;
}

Çözümleme *t 
iş türÖzetiEsnek Özet *imge::cins::özet: *imge::t 
{
  Şuanki := Çözümleme->şuanki(); 
  //Şuanki->Yaz("Tür özeti: ");
  Özet->Öz->KonumGüncelle(Çözümleme->şuanki());
  durum Şuanki->özellik:
  {
    seçim simge::Özellik::Eşittir,
          simge::Özellik::Virgül, 
          simge::Özellik::Noktalı_Virgül:
      dön Çözümleme->Hataİmla("Tür ifadesi bekleniyor.");
    
  }
  
  Çözümleme->TürÖzetiKonum(Özet); 
  Gelen := Çözümleme->türİfadesi(0);
  durum Gelen->özellik:
  {
    seçim imge::Özellik::Hata,
          imge::Özellik::Bildiri:
      dön Gelen;
    varsayılan:
      Özet->Gösterge = Gelen;
  }
  dön Çözümleme->türÖzetiDiziEsnek(Özet);
}