
Çözümleme *t 
iş yalın Tür *imge::cins::t: *imge::t 
{
  İmge := Tür->Öz; 
  durum Çözümleme->şuanki()->özellik:
  {
    seçim simge::Özellik::Noktalı_Virgül:
      Tür->özellik.türü = imge::cins::Özellik::Tanım;
      dön İmge;
  }
  Tür->özellik.türü = imge::cins::Özellik::Yalın;

  Hafıza := Çözümleme->hafıza();
  Ad := Hafıza->Harflerden('...');
  Değişken := imge::değişken::Yeni(Hafıza, Ad);
  Gelen := Çözümleme->türÖzeti(Değişken->Özet); 
  durum Gelen->özellik:
  {
    seçim imge::Özellik::Hata:
      dön İmge;
  }
  Tür->ÜyeEkle(Değişken->Öz); 

  durum Çözümleme->şuanki()->özellik:
  {
    seçim simge::Özellik::Noktalı_Virgül: 
      Çözümleme->sıradaki();
    varsayılan:
      dön Çözümleme->Hataİmla(
            "Yalın tür '%s' tanımı için noktalı virgül bekleniyor.");
  }
  dön İmge;
}



Çözümleme *t 
iş türDalları Tür *imge::cins::t : *imge::t 
{

  Simge := Çözümleme->şuanki(); 
  değer Gelen *imge::t           = boş;  

  her Çözümleme->Devam(): 
  {
    Simge = Çözümleme->şuanki();
    durum Simge->özellik: 
    {
      seçim simge::Özellik::Yorum:
        Simge = Çözümleme->sıradaki();
      seçim simge::Özellik::Küme_Kapa:
        Çözümleme->sıradaki();
        dön Tür->Öz;
      seçim simge::Özellik::Sözcük:
        Gelen = Çözümleme->değişken(Tür->Öz); 
    }
    eğer Gelen:
    {
      durum Gelen->özellik: 
      {
        seçim imge::Özellik::Değişken: 
          Tür->ÜyeEkle(Gelen);
        varsayılan:
      }
      durum Çözümleme->şuanki()->özellik:
      {
        seçim simge::Özellik::Noktalı_Virgül: 

         // Çözümleme->Şuanki()->Yaz(""); 
          Simge = Çözümleme->sıradaki();
          Gelen = boş;
        varsayılan:
          dön Çözümleme->Hataİmla("Tür üyesi tanımı için noktalı virgül bekleniyor.");
      }
    } 
    değilse: son;
  }
  dön Tür->Öz;
}

Çözümleme *t 
iş _tür özelleştirme mimari : *imge::t 
{
  özellik := Çözümleme->şuanki()->özellik;
  Simge := Çözümleme->sıradaki(); 
  değer İmge *imge::t           = boş; 
  değer Gelen *imge::t          = boş;
  değer Tür *imge::cins::t     = boş;
  durum Simge->özellik: 
  {
    seçim simge::Özellik::Sözcük:
      Tür = imge::cins::Yeni(
        Çözümleme->Hafıza, 
        Simge->içerik.Metin, 
        (seç özellik: 
          simge::Terim::Ortak: imge::cins::Özellik::Ortak,
          simge::Terim::Kalıp: imge::cins::Özellik::Kalıp,
          varsayılan:          imge::cins::Özellik::Varsayılan)); 
      Üst := Çözümleme->yığınlar.cins.Son(); 
      İmge = Tür->Öz; 
      İmge->KonumGüncelle(Çözümleme->şuanki());
      Çözümleme->sıradaki();
    varsayılan: 
      dön Çözümleme->Hataİmla("Tür tanımı için isim bekleniyor.");
  }
  
  sanallık := (özelleştirme & imge::Özelleştirme::Sanal);
  eğer sanallık == imge::Özelleştirme::Sanal:
  {
    durum Çözümleme->şuanki()->özellik:
    {
      seçim simge::Özellik::Sözcük:
        İmge->özellik = imge::Özellik::Kalıp;
        Tür->Donatım = 
          <*imge::cins::donatım>Çözümleme->Hafıza->Yeni(
            @imge::cins::donatım, @mimari); 
        Tür->Donatım->no = Tür->no; 
        Tür->özellik.türü =  imge::cins::Özellik::Kalıp;
        her i := 0; i < 2;:
        {
          durum Çözümleme->şuanki()->özellik:
          {
            seçim simge::Özellik::Virgül:
              Çözümleme->sıradaki();
            seçim simge::Özellik::Küme_Aç:
              Tür->Donatım->boyut = i;
              son;
            seçim simge::Özellik::Sözcük:
              Taç := imge::Adlı( 
                Çözümleme->Hafıza,
                Çözümleme->şuanki()->içerik.Metin, 
                imge::Özellik::Taç);
              Tür->Donatım->_sıra[Tür->Donatım->boyut] = Taç;
              Tür->Donatım->boyut++;
              Çözümleme->sıradaki();
              i++;
            varsayılan:
              dön Çözümleme->Hataİmla(
                "Sanal tür yanlış taçlandırılmış.");
          }
        }
      varsayılan: 
        dön Çözümleme->Hataİmla("Sanal tür tanımı için beklenmeyen simge.");
    }
  }
  
  İmge->Kütüphane = Çözümleme->yığınlar.kütüphane.Son();
  Çözümleme->yığınlar.cins.Ekle(Tür);
  durum Çözümleme->şuanki()->özellik: 
  {
    seçim simge::Özellik::Küme_Aç: 
    {
      Simge = Çözümleme->sıradaki();
      durum Simge->özellik:
      {
        seçim simge::Terim::Tür,
              simge::Terim::Ortak, 
              simge::Özellik::Sözcük: 
          Gelen := Çözümleme->türDalları(Tür); 
          eğer Gelen: 
            durum Gelen->özellik:
            {
              seçim imge::Özellik::Bildiri:
                dön Gelen;
            }
        varsayılan:
          dön Çözümleme->Hataİmla("Tür üyesi için beklenmeyen simge.");
      }
    }
    varsayılan:
      Çözümleme->yalın(Tür);
  }
  Çözümleme->yığınlar.cins.Çıkar();
  İmge->ÖnSıralamayaEkle();
  dön İmge;
} 

