dahili örs::merkez::iletişim: i; 
dahili örs::merkez::bellek;
Nesne *t 
küresel iş Çağrı 
  Üretim *üretim::t, 
  Konum *imge::işlem::konum,
  İşlemNesnesi *nesne::t,
  Yığın *_nesneler : *t  
{
 // i::Yaz("çağrı nesne %d\n", Yığın->boyut);
  değer Argüman *bellek::t = boş; 
  değer Geçici *nesne::t = boş;
  Genel := Üretim->Bölüm->Genel();
  eğer !Konum->Atıf: 
  {
    eğer Konum->Çıktı->HiçMi(): 
    {
      Üretim->Bölüm->Genel()->Yaz(
        "  call void %%%d(\n", İşlemNesnesi->no);
    }
    değilse: 
    {
      Argüman = Üretim->TürdenİkinciArgümana(&Konum->Çıktı->Öz->nesne);
      Nesne->no = Üretim->Değer();
      Üretim->Bölüm->Genel()->Yaz("  %%%d = call %s %%%d(%s", 
        Nesne->no, 
        Argüman->_veri, 
        İşlemNesnesi->no, (seç !Konum->girdi.boyut: 
          hayır: '\n', 
          varsayılan: ''));
    }
  }
  değilse: 
  {

    //i::Yaz("konum çıktı = %d, %s\n", Konum->Çıktı->HiçMi(), Konum->Atıf->nesne.Metin->_harfler);
    eğer Konum->Çıktı->HiçMi(): 
    {
      Üretim->Bölüm->Genel()->Yaz(
        "  call void @%s(\n", Konum->Atıf->nesne.Metin->_harfler);
    }
    değilse: 
    {
      Argüman = Üretim->TürdenİkinciArgümana(&Konum->Öz->nesne);
      Nesne->no = Üretim->Değer();
      Üretim->Bölüm->Genel()->Yaz("  %%%d = call %s @%s(%s", 
        Nesne->no, 
        Argüman->_veri, 
        Konum->Atıf->nesne.Metin->_harfler, (seç !Konum->Atıf: 
          hayır: '\n', 
          varsayılan: ''));
    }
  }
  eğer Yığın: 
  {
    boyut := <tam>Yığın->boyut;  
    değer Nesneler **t = &Yığın->_İçerik;
        //i::Acil("ilk %p: %d, %p\n", Yığın, boyut, Yığın->_İçerik[0]);
    her i := 0; i < boyut; i++:
    {
      Geçici = Nesneler[i]; 
      //i::Acil("geçici i %d %p: %d, %p\n", i, Yığın, boyut, Geçici);
      Argüman = Üretim->İlkArgüman(Geçici); 
      Genel->Yaz("    %s%s", Argüman->_veri, (seç i < (boyut -1):
        evet: ', \n', 
        varsayılan: ''));
    }
  }
  Genel->Yaz(")\n");
  Nesne->İşlemlendir(Makina::Çağrı); 
  Nesne->Anlamlandır(Anlam::Değer);
  dön Nesne;
}